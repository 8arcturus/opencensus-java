plugins {
  id 'com.github.johnrengelman.shadow' version '2.0.0'
}

description = 'OpenCensus Agent'

sourceCompatibility = 1.7
targetCompatibility = 1.7

def agentPackage = 'io.opencensus.contrib.agent'
def agentMainClass = "${agentPackage}.AgentMain"
def agentRepackaged = "${agentPackage}.deps"

dependencies {
  compile group: 'net.bytebuddy', name: 'byte-buddy', version: '1.7.1'

  signature 'org.codehaus.mojo.signature:java17:+@signature'
}

jar {
  manifest {
    // Set the required manifest attributes for the Java agent, cf.
    // https://docs.oracle.com/javase/8/docs/api/java/lang/instrument/package-summary.html.
    attributes 'Premain-Class': agentMainClass
    attributes 'Can-Retransform-Classes': true

    // Let the java plugin use the overridden values instead of the root project's values.
    attributes 'Source-Compatibility': sourceCompatibility
    attributes 'Target-Compatibility': targetCompatibility
  }
}

// Bundle the agent's classes and dependencies into a single, self-contained JAR file.
shadowJar {
  // Output to opencensus-agent-VERSION.jar.
  baseName = 'opencensus-agent'
  classifier = null

  // Include only the following dependencies (excluding transitive dependencies).
  dependencies {
    include(dependency('net.bytebuddy:byte-buddy'))
  }

  // Exclude cruft which still snuck in.
  exclude 'META-INF/maven/**'

  // Relocate third party packages to avoid any conflicts of the agent's classes with the app's
  // classes, which are loaded by the same classloader (the system classloader).
  relocate 'net.bytebuddy', agentRepackaged + '.bytebuddy'

  doLast {
    // Assert that there's nothing unexpected left outside of ${agentPackage}.
    def agentPackageDir = agentPackage.replace('.', '/') + '/'

    new java.util.zip.ZipFile(shadowJar.archivePath).withCloseable {
      it.entries().each {
        assert it.name.startsWith(agentPackageDir) ||
               (it.isDirectory() && agentPackageDir.startsWith(it.name)) ||
               it.name == 'META-INF/' ||
               it.name == 'META-INF/MANIFEST.MF'
      }
    }
  }
}

jar.finalizedBy shadowJar
